= Automate Flex Gateway in Connected Mode Using a Jenkins Pipeline
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:imagesdir: ../assets/images
:page-mode: conn

Using a Jenkins pipeline, you can automate the workflow for Flex Gateway in Connected Mode. With a Jenkins pipeline, you can register and run a Flex Gateway and utilize features of the https://anypoint.mulesoft.com/exchange/portals/anypoint-platform/f1e97bc6-315a-4490-82a7-23abe036327a.anypoint-platform/api-manager-api/[API Manager API] such as creating and deploying API instances.

Automating your Flex Gateway in Connected Mode work flow shares many similarities with the xref:flex-conn-manage-public-api.adoc[Manage Flex Gateway Using the API Manager API] tutorial. Using this Jenkins pipeline tutorial, you can automate the calls made to the API Manager API. To add additional features from the  https://anypoint.mulesoft.com/exchange/portals/anypoint-platform/f1e97bc6-315a-4490-82a7-23abe036327a.anypoint-platform/api-manager-api/[API Manager API], use the <<create-an-api-instance, Create an API Instance>> and <<apply-an-api-policy, Apply an API Policy>> pipeline stages as examples.

This tutorial has different steps with different code blocks. As you work through the steps, add the code blocks to the same Jenkins configuration file.

After you implement these pipeline stages, you can customize your pipeline by making different API Manager API requests.

== Before You Begin
Before you can deploy a Jenkins pipeline, complete the following tasks:

* xref:service-mesh::obtain-connected-apps-credentials.adoc[Configure a Connected App]:

** Use the *App acts on its behalf (client credentials)* type and include the following scopes:

*** API Manager: *Manage APIs Configuration*, *Manage Policies*, *View Policies*, and *Deploy API Proxies*
*** Runtime Manager: *Read Servers*, *Manage Servers*
*** Exchange: *Exchange Viewer*
*** General: *View Organization*

** Save the *Id* and *Secret* of the Connected app you configure.


* Collect the Group ID and Asset ID, and Asset Version (GAV) of the Exchange asset you want to create or apply.
+
To collect you GAV from Exchange:
+
. Go to Exchange.
. Find the asset to add or apply.
. Collect the Group ID and Asset ID from the URL.
+
For example, the GAV collected from the Basic Authentication Policy Exchange URL, `https://anypoint.mulesoft.com/exchange/68ef9520-24e9-4cf2-b2f5-620025690913/http-basic-authentication/minor/1.3/`, is:
+
*** Group ID: `68ef9520-24e9-4cf2-b2f5-620025690913`
*** Asset ID: `http-basic-authentication`
*** Asset Version: `1.3.0` (You can find this version clicking on `View Versions`)


* Collect the following information from your Anypoint Platform instance:

** The *Organization ID* for the organization where you want to run Flex Gateway
+
See xref:access-management::organization.adoc#find-your-organization-id[Find your Organization ID] for more information on how to find your Organization ID.

** The *Environment ID* for the environment where you want to run Flex Gateway
+
See xref:api-manager::latest-overview-concept.adoc#what-api-manager-looks-like[What API Manager Looks Like]
for more information on how to find your Environment ID.


== Define Useful Functions

To begin, define these useful functions in your Jenkinsfile:

[source,ssh]
----
def parseYamlFile(String file) {
   object = readYaml file: file
   return object
}

def parseJsonFile(file) {
   object = readJSON file: file
   return object
}

----

== Define Useful Parameters

Given that there are some input values needed for running the pipeline, here we define the necessary parameters
[source,ssh]
----
parameters {
   string(name: 'ENV_ID', defaultValue: '', description: 'Your environment id')
   string(name: 'ORG_ID', defaultValue: '', description: 'Your organization id')
   string(name: 'FLEX_VERSION', defaultValue: '', description: 'Flex version to run')
   string(name: 'CONNECTED_APP_CLIENT_ID', defaultValue: '', description: 'Your connected app client id')
   string(name: 'CONNECTED_APP_CLIENT_SECRET', defaultValue: '', description: 'Your connected app client secret')
   string(name: 'EXCHANGE_GROUP_ID', defaultValue: '', description: 'The group id of the http api exchange asset')
   string(name: 'EXCHANGE_ASSET_ID', defaultValue: '', description: 'The id of the http api exchange asset')
   string(name: 'EXCHANGE_ASSET_VERSION', defaultValue: '', description: 'The version of the http api exchange asset')
   string(name: 'GATEWAY_NAME', defaultValue: '', description: 'Unique gateway name')
}
----
== Check parameters
Add the `Check parameters` stage to your Jenkins pipeline:

[source,ssh]
----
stage('Check parameters') {
   steps {
       script {
           if (!params.ENV_ID || !params.ORG_ID || !params.FLEX_VERSION || !params.CONNECTED_APP_CLIENT_ID || !params.CONNECTED_APP_CLIENT_SECRET || !params.EXCHANGE_GROUP_ID || !params.EXCHANGE_ASSET_ID || !params.EXCHANGE_ASSET_VERSION || !params.GATEWAY_NAME)  {
               error("Not all parameters where specified")
           }
       }
   }
}
----
+
This stage validates if all parameters are present. If this stage fails, specify the missing parameters, then rerun the pipeline.

== Register and Run

Add the register and run stages to your Jenkins Pipeline:

[source,ssh]
----
stage('Register flex gateway') {
    steps {
        script {
            sh """
                docker run --entrypoint flexctl \
                    -v "\$(pwd)":/registration -u 0 mulesoft/flex-gateway:${params.FLEX_VERSION} \
                    register \
                    --client-id=${params.CONNECTED_APP_CLIENT_ID} \
                    --client-secret=${params.CONNECTED_APP_CLIENT_SECRET} \
                    --environment=${params.ENV_ID} \
                    --connected=true \
                    --organization=${params.ORG_ID} \
                    --output-directory=/registration \
                    ${params.GATEWAY_NAME}
                """
                if (!fileExists('registration.yaml')) {
                    error("Registration failed")
                }
        }
    }
}

stage('Run flex gateway') {
    steps {
        script {
            sh """
                docker run -d \
                -v "\$(pwd)":/usr/local/share/mulesoft/flex-gateway/conf.d \
                -p 8082:8082 \
                mulesoft/flex-gateway:${params.FLEX_VERSION} > containerId
            """
        }
    }
}
----
After registering and running your Flex Gateway, you must obtain a valid authorization token from Access Management in order to authenticate subsequent requests.

== Obtain an Authorization Token from Access Management
To login into Anypoint, add the `Anypoint login` stage to your Jenkins pipeline:
[source,ssh]
----
stage("Anypoint login") {
   steps {
       script {
           def cmd = """curl -s -w "%{http_code}" --location --request POST 'https://anypoint.mulesoft.com/accounts/api/v2/oauth2/token' -o 'login-response' \
                     --header 'Content-Type: application/json' \
                     --data-raw '{"grant_type": "client_credentials", "client_id": "${params.CONNECTED_APP_CLIENT_ID}", "client_secret": "${params.CONNECTED_APP_CLIENT_SECRET}"}'
                     """
           def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
           if (status_code > 399) {
               error("Anypoint login failed")
           }
       }
   }
}
----

The authorization token returned in this step is saved in a temporary file for subsequent requests.

== Create an API Instance
Having an authorization token from Access Management allows you to create an API Instance and deploy it to your Flex Gateway.

You can repeat this step to add multiple API Instances.

In this stage we create an API Instance that listens on port `8082` and `/httpbin` (as we can see in `proxyUri` field) path and proxies to `https://httpbin.org/`.

[source,ssh]
----
stage("Create API Instance") {
   steps {
       script {
           def login_response = parseJsonFile('login-response')


           def cmd = """
               curl -s -w "%{http_code}" --location --request POST 'https://anypoint.mulesoft.com/apimanager/api/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis' -o 'api-response' \
               --header 'Authorization: Bearer ${login_response.access_token}' \
               --header 'Content-Type: application/json' \
               --data-raw '{
                 "spec": {
                    "groupId": "${params.EXCHANGE_GROUP_ID}",
                    "assetId": "${params.EXCHANGE_ASSET_ID}",
                    "version": "${params.EXCHANGE_ASSET_VERSION}"
                 },
                 "endpoint": {
                    "deploymentType": "HY",
                    "uri": "https://httpbin.org:443",
                    "proxyUri": "http://0.0.0.0:8082/httpbin",
                    "isCloudHub": null
                 },
                 "technology": "flexGateway"
                }'
           """


           def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
           if (status_code > 399) {
               error("Error occurred while trying to create API Instance")
           }
       }
   }
}
----

== Apply a Basic Authentication Policy to the API Instance

After you create an API Instance, you can apply policies to the instance. In this case, we are applying a Basic Authentication Policy. For more information on how this works, see xref:flex-conn-manage-public-api.adoc[API Manager API].

Add the `Apply policy` stage to your Jenkins pipeline:
[source,ssh]
----
stage("Apply Basic Authentication Policy to API Instance") {
   steps {
       script {
           def login_response = parseJsonFile('login-response')
           def api_response = parseJsonFile('api-response')


           def cmd = """
               curl -s -w "%{http_code}" -o /dev/null --location --request POST 'https://anypoint.mulesoft.com/apimanager/api/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis/${api_response.id}/policies' \
               --header 'Authorization: Bearer ${login_response.access_token}' \
               --header 'Content-Type: application/json' \
               --data-raw '{
                 "configurationData":{
                    "username": "user",
                    "password": "test"
                 },
                 "pointcutData": null,
                 "assetId": "http-basic-authentication",
                 "assetVersion": "1.3.1",
                 "groupId": "68ef9520-24e9-4cf2-b2f5-620025690913"
                }'
           """


           def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
           if (status_code > 399) {
               error("Error occurred while trying to apply Basic Auth Policy to the API Instance")
           }
       }
   }
}
----

NOTE: You can only apply Connected Mode policies.

== Deploying an API Instance
After you create an API Instance, you can deploy the instance.

To deploy the instance, add the `Deploy API` stage to your Jenkins pipeline:

[source,ssh]
----
stage("Deploy API") {
    steps {
        script {
            def login_response = parseJsonFile('login-response')
            def api_response = parseJsonFile('api-response')
            def registration_file = parseYamlFile('registration.yaml')


            def cmd = """
                curl -s -w "%{http_code}" -o /dev/null --location --request POST 'https://anypoint.mulesoft.com/proxies/xapi/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis/${api_response.id}/deployments' \
                --header 'Content-Type: application/json' \
                --header 'Authorization: Bearer ${login_response.access_token}' \
                --data-raw '{
                "type": "HY",
                "gatewayVersion": "${params.FLEX_VERSION}",
                "targetId": "${registration_file.spec.platformConnection.agentId[0]}",
                "environmentId": "${params.ENV_ID}"
                }'
            """


            def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
            if (status_code > 399) {
                error("Error occurred while trying to deploy the API Instance")
            }
        }
    }
}
----

== Validate API Instance is Deployed
After you deploy your API Instance, you can validate if the instance is deployed.

To validate if the instance is deployed, add the `Validate deployment` stage to your Jenkins pipeline:


[source,ssh]
----
stage("Validate deployment") {
    steps {
        script {
            sleep(time:10, unit:"SECONDS")


            def unauthorized_status_code = sh(script: "curl -s -w \"%{http_code}\" -o /dev/null http://localhost:8082/httpbin/headers -v", returnStdout: true).trim().toInteger()
            if (unauthorized_status_code != 401) {
                error("401 status code expected but the status code obtained is ${unauthorized_status_code}")
            }


            def authorized_status_code = sh(script: "curl -s -w \"%{http_code}\" -o /dev/null http://localhost:8082/httpbin/headers -v -u user:test", returnStdout: true).trim().toInteger()
            if (authorized_status_code != 200) {
                error("200 status code expected but the status code obtained is ${unauthorized_status_code}")
            }
        }
    }
}
----


== Stop the Flex Gateway and output the registration file

Use the following code blocks to stop your Flex Gateway and output the registration file:

[source,ssh]
----
post {
   success {
       script {
           // Archive the build output artifacts.
           archiveArtifacts artifacts: 'registration.yaml'
       }
   }
   always {
       script {
           if (fileExists('containerId')) {
               sh(script: "docker rm -f ${readFile(file: 'containerId')}")
           }
       }
   }
}

----

== Full Jenkinsfile

[source,ssh]
----
def parseYamlFile(String file) {
  object = readYaml file: file
  return object
}

def parseJsonFile(file) {
  object = readJSON file: file
  return object
}

pipeline {
  agent any

  parameters {
      string(name: 'ENV_ID', defaultValue: '', description: 'Your environment id')
      string(name: 'ORG_ID', defaultValue: '', description: 'Your organization id')
      string(name: 'FLEX_VERSION', defaultValue: '', description: 'Flex version to run')
      string(name: 'CONNECTED_APP_CLIENT_ID', defaultValue: '', description: 'Your connected app client id')
      string(name: 'CONNECTED_APP_CLIENT_SECRET', defaultValue: '', description: 'Your connected app client secret')
      string(name: 'EXCHANGE_GROUP_ID', defaultValue: '', description: 'The group id of the http api exchange asset')
      string(name: 'EXCHANGE_ASSET_ID', defaultValue: '', description: 'The id of the http api exchange asset')
      string(name: 'EXCHANGE_ASSET_VERSION', defaultValue: '', description: 'The version of the http api exchange asset')
      string(name: 'GATEWAY_NAME', defaultValue: '', description: 'Unique gateway name')
  }

  stages {
      stage('Check parameters') {
          steps {
              script {
                  if (!params.ENV_ID || !params.ORG_ID || !params.FLEX_VERSION || !params.CONNECTED_APP_CLIENT_ID || !params.CONNECTED_APP_CLIENT_SECRET || !params.EXCHANGE_GROUP_ID || !params.EXCHANGE_ASSET_ID || !params.EXCHANGE_ASSET_VERSION || !params.GATEWAY_NAME)  {
                      error("Not all parameters where specified")
                  }
              }
          }
      }

      stage('Register flex gateway') {
          steps {
              script {
                  sh """
                      docker run --entrypoint flexctl \
                       -v ${pwd()}:/registration -u 0 mulesoft/flex-gateway:${params.FLEX_VERSION} \
                       register \
                       --client-id=${params.CONNECTED_APP_CLIENT_ID} \
                       --client-secret=${params.CONNECTED_APP_CLIENT_SECRET} \
                       --environment=${params.ENV_ID} \
                       --connected=true \
                       --organization=${params.ORG_ID} \
                       --output-directory=/registration \
                       ${params.GATEWAY_NAME}
                  """
                  if (!fileExists('registration.yaml')) {
                      error("Registration failed")
                  }
              }
          }
      }

      stage('Run flex gateway') {
          steps {
              script {
                  sh """
                      docker run -d \
                       -v ${pwd()}:/usr/local/share/mulesoft/flex-gateway/conf.d \
                       -p 8082:8082 \
                       mulesoft/flex-gateway:${params.FLEX_VERSION} > containerId
                  """
              }
          }
      }

      stage("Anypoint login") {
          steps {
              script {
                  def cmd = """curl -s -w "%{http_code}" --location --request POST 'https://anypoint.mulesoft.com/accounts/api/v2/oauth2/token' -o 'login-response' \
                            --header 'Content-Type: application/json' \
                            --data-raw '{"grant_type": "client_credentials", "client_id": "${params.CONNECTED_APP_CLIENT_ID}", "client_secret": "${params.CONNECTED_APP_CLIENT_SECRET}"}'
                            """
                  def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
                  if (status_code > 399) {
                      error("Anypoint login failed")
                  }
              }
          }
      }

      stage("Create API Instance") {
          steps {
              script {
                  def login_response = parseJsonFile('login-response')

                  def cmd = """
                      curl -s -w "%{http_code}" --location --request POST 'https://anypoint.mulesoft.com/apimanager/api/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis' -o 'api-response' \
                      --header 'Authorization: Bearer ${login_response.access_token}' \
                      --header 'Content-Type: application/json' \
                      --data-raw '{
                        "spec": {
                           "groupId": "${params.EXCHANGE_GROUP_ID}",
                           "assetId": "${params.EXCHANGE_ASSET_ID}",
                           "version": "${params.EXCHANGE_ASSET_VERSION}"
                        },
                        "endpoint": {
                           "deploymentType": "HY",
                           "uri": "https://httpbin.org:443",
                           "proxyUri": "http://0.0.0.0:8082/httpbin",
                           "isCloudHub": null
                        },
                        "technology": "flexGateway"
                       }'
                  """

                  def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
                  if (status_code > 399) {
                      error("Error occurred while trying to create API Instance")
                  }
              }
          }
      }

      stage("Apply Basic Authentication Policy to API Instance") {
          steps {
              script {
                  def login_response = parseJsonFile('login-response')
                  def api_response = parseJsonFile('api-response')

                  def cmd = """
                      curl -s -w "%{http_code}" -o /dev/null --location --request POST 'https://anypoint.mulesoft.com/apimanager/api/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis/${api_response.id}/policies' \
                      --header 'Authorization: Bearer ${login_response.access_token}' \
                      --header 'Content-Type: application/json' \
                      --data-raw '{
                        "configurationData":{
                           "username": "user",
                           "password": "test"
                        },
                        "pointcutData": null,
                        "assetId": "http-basic-authentication",
                        "assetVersion": "1.3.1",
                        "groupId": "68ef9520-24e9-4cf2-b2f5-620025690913"
                       }'
                  """

                  def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
                  if (status_code > 399) {
                      error("Error occurred while trying to apply Basic Auth Policy to the API Instance")
                  }
              }
          }
      }

      stage("Deploy the API") {
          steps {
              script {
                  def login_response = parseJsonFile('login-response')
                  def api_response = parseJsonFile('api-response')
                  def registration_file = parseYamlFile('registration.yaml')

                  def cmd = """
                      curl -s -w "%{http_code}" -o /dev/null --location --request POST 'https://anypoint.mulesoft.com/proxies/xapi/v1/organizations/${params.ORG_ID}/environments/${params.ENV_ID}/apis/${api_response.id}/deployments' \
                      --header 'Content-Type: application/json' \
                      --header 'Authorization: Bearer ${login_response.access_token}' \
                      --data-raw '{
                       "type": "HY",
                       "gatewayVersion": "${params.FLEX_VERSION}",
                       "targetId": "${registration_file.spec.platformConnection.agentId[0]}",
                       "environmentId": "${params.ENV_ID}"
                      }'
                  """

                  def status_code = sh(returnStdout: true, script: cmd).trim().toInteger()
                  if (status_code > 399) {
                      error("Error occurred while trying to deploy the API Instance")
                  }
              }
          }
      }

      stage("Validate Deployment") {
          steps {
              script {
                  sleep(time:10, unit:"SECONDS")

                  def unauthorized_status_code = sh(script: "curl -s -w \"%{http_code}\" -o /dev/null http://localhost:8082/httpbin/headers -v", returnStdout: true).trim().toInteger()
                  if (unauthorized_status_code != 401) {
                      error("401 status code expected but the status code obtained is ${unauthorized_status_code}")
                  }

                  def authorized_status_code = sh(script: "curl -s -w \"%{http_code}\" -o /dev/null http://localhost:8082/httpbin/headers -v -u user:test", returnStdout: true).trim().toInteger()
                  if (authorized_status_code != 200) {
                      error("200 status code expected but the status code obtained is ${unauthorized_status_code}")
                  }
              }
          }
      }
  }

  post {
      success {
          script {
              // Archive the build output artifacts.
              archiveArtifacts artifacts: 'registration.yaml'
          }
      }
      always {
          script {
              if (fileExists('containerId')) {
                  sh(script: "docker rm -f ${readFile(file: 'containerId')}")
              }
          }
      }
  }
}
----

== See Also

* xref:flex-install.adoc[Install Flex Gateway].
* xref:flex-{page-mode}-reg-run.adoc[Register and Run Flex Gateway].
* https://anypoint.mulesoft.com/exchange/portals/anypoint-platform/f1e97bc6-315a-4490-82a7-23abe036327a.anypoint-platform/api-manager-api/[Anypoint Developer API Manager API]
* xref:flex-conn-manage-public-api.adoc[Manage Flex Gateway Using the API Manager API]